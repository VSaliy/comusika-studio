apply plugin: 'base'
apply plugin: 'distribution'
apply plugin: 'com.bmuschko.izpack'

version = '0.1.0-SNAPSHOT'

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '4.0'
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

distZip.dependsOn project(':tools').subprojects.assemble
distZip.dependsOn project(':tools:frinika').tasks['launch4j']
distTar.dependsOn project(':tools').subprojects.assemble
distTar.dependsOn project(':tools:frinika').tasks['launch4j']


distributions {
    main {
        contents {
            duplicatesStrategy = 'exclude'

            from project.rootDir
            include 'license.txt'
            include 'changes.txt'
            include 'frinika-changes.txt'
            from 'src/dist'
            include 'readme.txt'
            include 'frinika-studio.bat'
            include 'frinika-studio.sh'

            from project(':tools:frinika').projectDir.absolutePath + '/build/launch4j'
            include "frinika-studio.exe"

            into('lib') {
                from 'lib'
                include '*.txt'
                include 'native/**'
            }
            project(':modules').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.jar.archivePath
                        include '*'
                        from p.configurations.runtime
                        include '*'
                    }
                }
            }
            project(':tools').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.configurations.runtime
                        include '*'
                    }

                    into('') {
                        from p.jar.archivePath
                        include '*'
                        rename { filename -> p.name + "-studio.jar" }
                    }
                }
            }
            into('doc') {
                from 'doc'
                include '**'
            }
            into('samples') {
                from 'resources/samples'
                include '**'
            }
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
    }
}

izpack {
    baseDir = file("$buildDir/distributions")
    installFile = file('src/install-definition.xml')
    outputFile = file("$buildDir/distributions/frinika-studio-${version}-installer.jar")
    compression = 'deflate'
    compressionLevel = 9
    appProperties = ['app.group': 'frinika-studio', 'app.name': 'frinika-studio', 'app.title': 'Frinika Studio',
                     'app.version': version, 'app.subpath': "frinika-studio-$version"]
}