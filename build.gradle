apply plugin: 'base'
apply plugin: 'distribution'
apply plugin: 'com.bmuschko.izpack'

def baseVersion = '0.1.1'
def devVersion = 'DEV'
def fullVersion = devVersion == '' ? baseVersion : baseVersion + ' ' + devVersion
version = (devVersion == '' ? baseVersion : baseVersion + '-SNAPSHOT') + '-' + getDate('yyyyMMdd')
def executableName = "frinika-studio"

def getDate(dateFormat) {
    def date = new Date()
    def formattedDate = date.format(dateFormat)
    return formattedDate
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }

task versionFile() {
    doLast {
        new File("$projectDir/modules/frinika-core/src/main/resources/version.properties").text = """\n\
baseVersion = $baseVersion
version = $fullVersion
build-date = ${getDate('YYYY-MM-dd')}
copyrightStart = 2004
copyrightEnd = ${getDate('YYYY')}
executableName = $executableName
"""
    }
}

assemble.dependsOn project.tasks['versionFile']
distZip.dependsOn project(':tools').subprojects.assemble
distZip.dependsOn project(':tools:studio-app').tasks['createExe']
distTar.dependsOn project(':tools').subprojects.assemble
distTar.dependsOn project(':tools:studio-app').tasks['createExe']

distributions {
    main {
        contents {
            duplicatesStrategy = 'exclude'

            from project.rootDir
            include 'license.txt'
            include 'changes.txt'
            include 'frinika-changes.txt'
            into('') {
                from 'src/dist'
                include '**'
            }
            into('resources') {
                from 'tools/studio-app/resources'
                include 'soundhelix/**'
                include 'soundhelix-legacy/**'
            }

            from project(':tools:studio-app').projectDir.absolutePath + '/build/launch4j'
            include executableName + ".exe"

            into('lib') {
                from 'lib'
                include '*.txt'
            }
            into('lib/native') {
                from 'lib/native'
                include '**'
            }

            project(':modules').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.jar.archivePath
                        include '*'
                        from p.configurations.runtime
                        include '*'
                    }
                }
            }
            project(':tools').subprojects.each { p ->
                p.plugins.withType(JavaPlugin) {
                    into('lib') {
                        from p.configurations.runtime
                        include '*'
                    }

                    into('') {
                        from p.jar.archivePath
                        include '*'
                        rename { filename -> executableName + ".jar" }
                    }
                }
            }
            into('doc') {
                from 'doc'
                exclude 'dev'
                include '**'
            }
            into('samples') {
                from 'resources/samples'
                include '**'
            }
        }
    }
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
    }
}

dependencies {
    izpack 'org.codehaus.izpack:izpack-standalone-compiler:4.3.5'
}

izpack {
    baseDir = file("$buildDir/distributions")
    installFile = file('src/install-definition.xml')
    outputFile = file("$buildDir/distributions/frinika-studio-${version}-installer.jar")
    compression = 'deflate'
    compressionLevel = 9
    appProperties = ['app.group': 'frinika-studio', 'app.name': 'frinika-studio', 'app.title': 'Frinika Studio',
                     'app.version': version, 'app.subpath': "frinika-studio-$version"]
}